%%  A sentence is formed by a noun phrase followed by a verb phrase.
%%  The tense of the verb phrase must be the same as the tense of
%%  the sentence.

xS(Tense, Num, Person) --> xNP(Tense, Num, Person), xVP(Tense, Num, Person).

xNP(Tense, Num, Person) --> xAP, xPropNoun(Tense, Num, Person).
xNP(Tense, Num, Person) --> xDet(Tense, Num, Person), xAP, xNoun(Tense, Num, Person).
xNP(Tense, Num, Person) --> xPronoun(Tense, Num, Person).

xIObj(Tense, Num, Person) --> xPrep, xAP, xPropNoun(Tense, Num, Person).
xIObj(Tense, Num, Person) --> xPrep, xDet(Tense, Num, Person), xAP, xNoun(Tense, Num, Person).
xIObj(_, _, _) --> xPrep, xObjPronoun.

xVP(Tense, Num, Person) --> xIVerb(Tense, Num, Person).
xVP(Tense, Num, Person) --> xIVerb(Tense, Num, Person), xIObj(_, _, _).

xVP(Tense, Num, Person) --> xTVerb(Tense, Num, Person), xDet(Tense2, Num2, Person2), xAP, xNoun(Tense2, Num2, Person2).
xVP(Tense, Num, Person) --> xTVerb(Tense, Num, Person), xAP, xPropNoun(_, _, _).
xVP(Tense, Num, Person) --> xTVerb(Tense, Num, Person), xObjPronoun.


xIVP(intran) --> xInfVerb(intran).
xIVP(intran) --> xInfVerb(intran), xIObj(_, _, _).

xIVP(tran) --> xInfVerb(tran), xDet(Tense2, Num2, Person2), xAP, xNoun(Tense2, Num2, Person2).
xIVP(tran) --> xInfVerb(tran), xAP, xPropNoun(_, _, _).
xIVP(tran) --> xInfVerb(tran), xObjPronoun.


xAP --> [].
xAP --> xAdj.
xAP --> xAdj, xAdj.



xQ(Tense, Num, Person) --> xHelper(Tense, Num, Person), xNP(Tense, Num, Person), xIVP(_).



%%------------------------------------------------------------------------------------------

%% Some words...

xDet(_, singular, third) --> [a].
xDet(_, _, third) --> [the].
xDet(_, plural, third) --> [some].
xDet(_, plural, third) --> [].

xNoun(_, singular, third) --> [woman].
xNoun(_, plural, third) --> [women].

%xNoun(_, singular, third) --> [man].
%xNoun(_, plural, third) --> [men].

xPropNoun(_, singular, third) --> [april, ludgate].
xPropNoun(_, singular, third) --> [andy, dwyer].
%xPropNoun(_, singular, third) --> [ron, swanson].

% Subject pronouns

xPronoun(_, _, second) --> [you].
xPronoun(_, singular, first) --> [i].
xPronoun(_, plural, first) --> [we].
xPronoun(_, plural, third) --> [they].
xPronoun(_, singular, third) --> [she].
xPronoun(_, singular, third) --> [he].

% Indirect object pronouns:

xObjPronoun --> [us].
xObjPronoun --> [me].
xObjPronoun --> [him].
xObjPronoun --> [her].
xObjPronoun --> [them].
xObjPronoun --> [you].

%% Some more words...

xPrep --> [to].
%xPrep --> [from].
%xPrep --> [around].

%%  Individual words.  Here, the tense of a given word is
%%  specified by a constant (e.g., past or present).

xIVerb(present, singular, third) --> [runs].
xIVerb(present, singular, first) --> [run].
xIVerb(present, plural, _) --> [run].
xIVerb(past, _, _) --> [ran].

xTVerb(present, singular, third) --> [trips].
xTVerb(present, singular, first) --> [trip].
xTVerb(present, singular, second) --> [trip].
xTVerb(present, plural, _) --> [trip].
xTVerb(past, _, _) --> [tripped].

xInfVerb(intran) --> [run].
xInfVerb(tran) --> [trip].

%%  xHelper:  holds for the helper verbs "does", "did", etc.

xHelper(present, singular, third) --> [does].
xHelper(present, singular, second) --> [do].
xHelper(present, singular, first) --> [do].
xHelper(present, plural, _) --> [do].
xHelper(past, _, _) --> [did].



xAdj --> [cute].
%xAdj --> [funny].
%xAdj --> [stupid].


%% xS(D, E, F, G, []). how to generate a random sentence
%% xS(present, plural, second, A, []). how to get a specific type of sentence
